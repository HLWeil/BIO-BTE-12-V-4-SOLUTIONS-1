
        {
            "cells": [
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["// can\u0027t yet format YamlFrontmatter ([\"title: 07 Datavisualization with Plotly.NET\"; \"category: Musterlösungen(deutsch)\"; \"categoryindex: 1\"; \"index: 5\"], Some { StartLine = 2 StartColumn = 0 EndLine = 6 EndColumn = 8 }) to pynb markdown\n",
"\n",
"[![Binder](/BIO-BTE-12-V-4-SOLUTIONS/img/badge-binder.svg)](https://mybinder.org/v2/gh/csbiology/BIO-BTE-12-V-4/gh-pages?filepath=task-solutions/german/wpb-task-07-solution.ipynb)\u0026emsp;\n",
"[![Script](/BIO-BTE-12-V-4-SOLUTIONS/img/badge-script.svg)](/BIO-BTE-12-V-4-SOLUTIONS/task-solutions/german/wpb-task-07-solution.fsx)\u0026emsp;\n",
"[![Notebook](/BIO-BTE-12-V-4-SOLUTIONS/img/badge-notebook.svg)](/BIO-BTE-12-V-4-SOLUTIONS/task-solutions/german/wpb-task-07-solution.ipynb)\n",
"\n",
"# Datenvisualisierung mit Plotly.NET\n",
"\n",
"**Verwenden Sie fuer alle Diagramme stets adequate Skalenbeschriftungen.**\n",
"\n",
"**Der Code fuer die Erstellung der Charts reicht aus, Sie muessen keine Bilder einreichen.**\n",
"\n",
"## 0 Vorwort\n",
"\n",
"Die Plotly.NET Dokumentation finden sie hier: https://plotly.net\n",
"\n",
"### Referenzieren von Plotly.NET\n",
"\n",
"Diese Zeilen müssen immer mindestens 1 mal ausgeführt werden, sonst kann das Plotly.NET Softwarepaket nicht verwendet werden:\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 1, "outputs": [], 
           "source": ["#r \"nuget: Plotly.NET, 2.0.0-preview.1\"\n",
"#r \"nuget: Plotly.NET.Interactive, 2.0.0-preview.1\"\n",
"open Plotly.NET\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["Bei dieser Uebung unterscheiden sich die Arbeitsweisen bezueglich des Anzeigens der erstellten Diagramme in Notebooks und .fsx Skripten grundlegend:\n",
"\n",
"### Anzeigen von Charts in .fsx Skripten\n",
"\n",
"In .fsx Skripten sollte die `Chart.Show` Funktion verwendet werden, welche ein Browserfenster öffnet um Diagramme anzuzeigen:\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": null, "outputs": [], 
           "source": ["Chart.Point([(1,1); (2,2)])\n",
"|\u003e Chart.withTitle \"hello from .fsx\"\n",
"|\u003e Chart.Show\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Anzeigen von Charts in Notebooks\n",
"\n",
"In Notebooks kann zwar auch die Chart.Show Funktion verwendet werden, dank der oben referenzierten `Plotly.NET.Interactive` \n",
"Erweiterung kann allerdings auch einfach der jeweilige Codeblock mit dem value des Charts beendet werden (so wie auch bei anderen Werten gewohnt), \n",
"um den Chart direkt in der Ausgabezelle anzuzeigen:\n",
"\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 2, "outputs": [], 
           "source": ["Chart.Point([(1,1); (2,2)])\n",
"|\u003e Chart.withTitle \"hello inside the notebook\"\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["## 1 Visualisierung von X/Y Beziehungen\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 3, "outputs": [], 
           "source": ["//Ich erstelle mir generell immer ein paar funktionen zum stylen von Charts. Ich mag zum beispiel gespiegelte achsen, die den plot einrahmen.\n",
"//diese funktion kann ich dann für alle Charts im projekt benutzen. Das war natürlich nicht von euch verlangt, ich packe es aber als\n",
"//\"real world\" example dazu.\n",
"\n",
"let myLinearAxis title showGrid = \n",
"    Axis.LinearAxis.init(\n",
"        Title = title,\n",
"        //Zeige hilfslinien nur wenn ich das sage\n",
"        Showgrid=showGrid,\n",
"        //Achse als durchgezogene linie\n",
"        Showline=true,\n",
"        //spiegelt die Achsen auf beiden seiten des plots\n",
"        Mirror=StyleParam.Mirror.All,\n",
"        Tickmode=StyleParam.TickMode.Auto,\n",
"        //Striche der Skala sind im Plot\n",
"        Ticks= StyleParam.TickOptions.Inside, \n",
"        //Stylen des titels\n",
"        Tickfont=Font.init(StyleParam.FontFamily.Arial,Size=16.),\n",
"        Titlefont=Font.init(StyleParam.FontFamily.Arial,Size=20.)\n",
"    )\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Task 1.1\n",
"\n",
"Erstellen Sie eine Liste, welche die Werte von 0.0 bis (4 * PI) mit einer Schrittgroesse von 0.001 enthaelt.\n",
"binden Sie diese liste an den Namen \u0027xValues\u0027\n",
"\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 4, "outputs": [], 
           "source": ["let xValues = [0.0 .. 0.001 .. 4. * System.Math.PI]\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Task 1.2\n",
"\n",
"Erstellen Sie zwei Listen, die die passenden Funktionswerte von sin(x) und cos(x) fuer \u0027xValues\u0027 enthalten.\n",
"\n",
"_Tipp: benutzen Sie die map Funktion_\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 5, "outputs": [], 
           "source": ["let sinValues =\n",
"    xValues\n",
"    |\u003e List.map sin\n",
"\n",
"let cosValues = \n",
"    xValues\n",
"    |\u003e List.map cos\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Task 1.3\n",
"\n",
"Erstellen Sie zwei Punkt- und zwei Liniendiagramme in denen Sie die Werte von \u0027xValues\u0027 und die jeweiligen\n",
"Funktionswerte von sin(x) oder cos(x) auf je eine Positionsskala abbilden. Am Ende sollten Sie 4 einzelne \n",
"Diagramme haben.\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 6, "outputs": [], 
           "source": ["//Da ihr die charts 2 mal verwenden musstet (einzeln und kombiniert), macht es sinn sie an einen namen zu binden\n",
"//um sie wiederverwenden zu können.\n",
"\n",
"let sinLines = \n",
"    Chart.Line(xValues,sinValues,Name=\"sinLines\")\n",
"    |\u003e Chart.withX_AxisStyle \"x\"\n",
"    |\u003e Chart.withY_AxisStyle \"sin(x)\"\n",
"    |\u003e Chart.withTitle \"sin (x)\"\n",
"\n",
"//hier könnt ihr die custom achse von oben in action sehen.\n",
"//Da es sich um einen Axis.LinearAxis typ handelt, benutze ich hier withY_Axis anstatt withY_AxisStyle\n",
"let cosLines = \n",
"    Chart.Line(xValues,cosValues,Name=\"cosLines\")\n",
"    |\u003e Chart.withY_Axis (myLinearAxis \"x\" false)\n",
"    |\u003e Chart.withX_Axis (myLinearAxis \"cos(x)\" false)\n",
"    |\u003e Chart.withTitle \"cos (x)\"   \n",
"\n",
"let sinPoints = \n",
"    Chart.Point(xValues,sinValues,Name=\"sinPoints\")\n",
"    |\u003e Chart.withX_AxisStyle \"x\"\n",
"    |\u003e Chart.withY_AxisStyle \"sin(x)\"\n",
"    |\u003e Chart.withTitle \"sin (x)\"\n",
"\n",
"let cosPoints = \n",
"    Chart.Point(xValues,cosValues,Name=\"cosPoints\")\n",
"    |\u003e Chart.withX_AxisStyle \"x\"\n",
"    |\u003e Chart.withY_AxisStyle \"cos(x)\"\n",
"    |\u003e Chart.withTitle \"cos (x)\"\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Task 1.4\n",
"\n",
"**a)** Kombinieren Sie die Punkt- und Liniendiagramme aus 1.3. in einem einzigen Koordinatensystem (zu einem einzigen Diagramm mit 4 Unterdiagrammen). \n",
"\n",
"_Tipp: Benutzen Sie Chart.Combine_\n",
"\n",
"**b)** Warum ist das Resultat ein Diagramm mit einer zusaetzlichen Dimension, bzw. was ist die zusätzlich abgebildete Dimension?\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 7, "outputs": [], 
           "source": ["//Die punkte sind mir in dem plot zu gro?, also nutze ich Chart.withMarkewrStyle um sie zu verändern.\n",
"\n",
"[\n",
"    sinPoints\n",
"    |\u003e Chart.withMarkerStyle(5,Symbol = StyleParam.Symbol.Cross)\n",
"    cosPoints\n",
"    |\u003e Chart.withMarkerStyle(5,Symbol = StyleParam.Symbol.Cross)\n",
"    sinLines\n",
"    cosLines\n",
"]\n",
"|\u003e Chart.Combine\n",
"|\u003e Chart.withX_Axis (myLinearAxis \"x\" false)\n",
"|\u003e Chart.withY_Axis (myLinearAxis \"f(x)\" false)\n",
"|\u003e Chart.withTitle \"cos / sin (x)\"\n",
"\n",
"//1.4b: Die zusätzliche Dimension entsteht durch die Farbgebung für die unterschiedlichen Traces im Plot.\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Task 1.5 \n",
"\n",
"Visualisieren Sie die (x,y) tuple in der folgenden Liste zunaechst mit einer linearen X und Y Skala.\n",
"Transformieren Sie die Daten mit einer nichtlinearen Transformation, die Ihnen sinnvoll erscheint und \n",
"visualisieren Sie das Ergebnis. \n",
"\n",
"_Tipp: sollten Sie anstatt der Daten die Achse transformieren wollen:_\n",
"`Chart.withY_Axis( Axis.LinearAxis.init(StyleParam.AxisType. ...)  )`\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 8, "outputs": [], 
           "source": ["let someData = [0. .. 1. .. 10.] |\u003e List.map (fun x -\u003e x,(10. ** x))\n",
"\n",
"//Schaut euch am besten immer zuerst die rohdaten an:\n",
"someData\n",
"|\u003e Chart.Point\n",
"\n",
"//Man sieht sehr gut wie weit die datenpunkte auseinander liegen.\n",
"\n",
"//Chart.Spline interpoliert die linie zwischen den Punkten, dann sieht das ganze smoother aus als mit Chart.Line\n",
"//Hier wird die Achse logarithmiert, beachtet dass hier die achse den titel \"y\" behält\n",
"Chart.Spline(someData)\n",
"|\u003e Chart.withY_Axis (Axis.LinearAxis.init(StyleParam.AxisType.Log))\n",
"|\u003e Chart.withY_AxisStyle \"y\"\n",
"|\u003e Chart.withX_AxisStyle \"x\"\n",
"\n",
"//Hier werden die y-Werte logarithmiert, beachtet dass hier die achse den titel \"log(y)\" erh�lt.\n",
"let transformedData = \n",
"    someData\n",
"    |\u003e List.map (fun (x,y) -\u003e (x,log y))\n",
"\n",
"Chart.Spline(transformedData)\n",
"|\u003e Chart.withX_AxisStyle \"x\"\n",
"|\u003e Chart.withY_AxisStyle \"log (y)\"\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["## 2 Visualisierung von Anzahl/Groessen\n",
"\n",
"### Task 2.1 \n",
"\n",
"Visualisieren Sie die Abundanz verschiedener Haarfarben der Teilnehmer des Kurses. Falls keine Erhebung \n",
"gemacht werden soll, denken Sie sich einen Datensatz mit 5 verschiedenen Haarfarben und 20 Probanden aus.\n",
"\n",
"_Tipp: erstellen Sie entweder eine Liste mit (Haarfarbe,Abundanz) tupeln oder je eine Liste fuer Haarfarbe und Abundanz_\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 9, "outputs": [], 
           "source": ["let haarfarben = [\n",
"    \"Schwarz\", 9\n",
"    \"Rot\"    , 2\n",
"    \"Gelb\"   , 1\n",
"    \"Braun\"  , 7\n",
"    \"Grau\"   , 1\n",
"]\n",
"\n",
"Chart.Column(haarfarben)\n",
"|\u003e Chart.withX_Axis (myLinearAxis \"Farbe\" false)\n",
"|\u003e Chart.withY_Axis (myLinearAxis \"Abundanz\" true)\n",
"|\u003e Chart.withTitle \"random haarfarben\"\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Task 2.2\n",
"\n",
"Visualisieren Sie die Menge an erzeugten Terawattstunden in Deutschland nach verschiedenen Energietraeger im Jahr 2020 hinsichtlich der absoluten Zahlen als Balkendiagramm, sortiert nach Groesse der Strommenge\n",
"\n",
"_Quelle der Daten:_ https://de.wikipedia.org/wiki/Stromerzeugung#Bruttostromerzeugung_nach_Energietr%C3%A4gern_in_Deutschland\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 10, "outputs": [], 
           "source": ["let terawattstundenDeutschland2020 = \n",
"    [\n",
"        91.7,\"Braunkohle\"           \n",
"        107.0,\"Windenergie, onshore\" \n",
"        91.6 ,\"Erdgas\"               \n",
"        64.3,\"Kernenergie\"          \n",
"        42.5,\"Steinkohle\"           \n",
"        51.,\"Photovoltaik\"         \n",
"        44.4,\"Biomasse\"             \n",
"        24.5,\"uebrige Energietraeger\" \n",
"        18.5,\"Windenergie, offshore\"\n",
"        18.7,\"Wasserkraft\"          \n",
"        5.9,\"Hausmuell\"            \n",
"        4.2,\"Mineraloelprodukte\"   \n",
"    ]\n",
"    |\u003e List.sortBy fst\n",
"\n",
"\n",
"Chart.Point(terawattstundenDeutschland2020)\n",
"|\u003e Chart.withMarginSize(Left=250.)\n",
"|\u003e Chart.withMarkerStyle 20\n",
"|\u003e Chart.withX_Axis (myLinearAxis \"Erzeugte Energie [tWh]\" false)\n",
"//Bei solchen plots, bei denen eine kategorische Beschriftung pro einheitenschritt vorhanden ist, kann man meist \n",
"//die generelle achsenbeschriftung weglassen (e.g. hier muss man nicht \"Energieträger\" dazuschreiben, wenn das aus titel und beschriftung gut hervorgeht)\n",
"|\u003e Chart.withY_Axis (myLinearAxis \"\" true)\n",
"|\u003e Chart.withTitle \"Erzeugte Terrawattstunden von verschiedenen Energietraegern in Deutschland \"\n",
"|\u003e Chart.withSize(1000.,750.)\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Task 2.3 \n",
"\n",
"Datenvisialisierung kann oft zur Erkennung von Mustern in einem Datensatz fuehren, die bei blosser \n",
"Betrachtung der Werte nicht ersichtlich waeren, besonders wenn der Datensatz sehr gross und/oder \n",
"mehrdimensional ist. \n",
"\n",
"Der vorbereitete Testdatensatz enthaelt 3 Dimensionen: die Indices des aeusseren Arrays, die Indices der \n",
"inneren Arrays, und die tatsaechlichen Zahlenwerte in den inneren Arrays. Visualisieren Sie die drei \n",
"Dimensionen mithilfe einer Heatmap.\n",
"\n",
"Welche Information koennte in den 3 Dimensionen kodiert sein? Beschriften Sie die x und y Skalen nach Ihrer \n",
"Einschaetzung.\n",
"\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 11, "outputs": [], 
           "source": ["let data3D =\n",
"    [|\n",
"        [|2.;2.;2.;2.;2.;2.;2.;2.;2.|]\n",
"        [|2.;2.;0.;0.;1.;0.;0.;2.;2.|]\n",
"        [|2.;0.;0.;3.;3.;3.;0.;0.;2.|]\n",
"        [|2.;0.;3.;0.;0.;0.;3.;0.;2.|]\n",
"        [|2.;0.;3.;0.;0.;0.;3.;0.;2.|]\n",
"        [|2.;0.;0.;0.;3.;0.;0.;0.;2.|]\n",
"        [|2.;0.;0.;0.;0.;0.;0.;0.;2.|]\n",
"        [|2.;0.;3.;0.;0.;0.;3.;0.;2.|]\n",
"        [|2.;2.;0.;0.;0.;0.;0.;2.;2.|]\n",
"        [|2.;2.;2.;1.;1.;1.;2.;2.;2.|]\n",
"    |]\n",
"\n",
"Chart.Heatmap(data=data3D)\n",
"|\u003e Chart.withX_AxisStyle(title=\"X-Koordinate des Pixels\")\n",
"|\u003e Chart.withY_AxisStyle(title=\"Y-Koordinate des Pixels\")\n",
"|\u003e Chart.withColorBarStyle(\"Pixel Intensität\")\n",
"\n",
"//Ich habe die intensität von bildpixeln in zahlen umgewandelt und das als 2D-Array\n",
"//bereitgestellt. Die x/y achsen sind die horizontalen/vertikalen pixel indices und die farbskala bildet die pixelintensität ab.\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["## 3   Visualisierung von Proportionen\n",
"\n",
"### Task 3.1 \n",
"\n",
"Visualisieren Sie das Ergebnis der Landtagswahl in einem beliebigen Bundesland.\n",
"\n",
"_Quelle der Daten:_ https://de.wikipedia.org/wiki/Liste_der_letzten_Landtagswahlergebnisse_in_Deutschland\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 12, "outputs": [], 
           "source": ["let wahlergebnisse,parteinamen,farben =\n",
"    [\n",
"        (27.7,\"CDU/CSU\",\"black\")\n",
"        (35.7,\"SPD\",\"#E3000F\")\n",
"        (9.3 ,\"Grüne\", \"#46962B\")\n",
"        (8.3 ,\"Afd\", \"#009EE0\")\n",
"        (5.5 ,\"FDP\", \"#FFFF00\")\n",
"        (2.5 ,\"Linke\", \"#BE3075\")\n",
"        (5.4 ,\"Freie Wähler/BVB/FW\", \"#FF8000\")\n",
"        (1.1 ,\"Die PARTEI\", \"darkred\")\n",
"        (0.5 ,\"Piraten\", \"orange\")\n",
"        (1.7 ,\"Tierschutzpartei\", \"grey\")\n",
"        (0.7 ,\"ÖDP\", \"grey\")\n",
"        (2.4 ,\"sonstige\", \"grey\")\n",
"    ]\n",
"    |\u003e List.unzip3\n",
"\n",
"\n",
"Chart.Pie(values=wahlergebnisse,Labels=parteinamen)\n",
"//Hier das setzen der Farben, wie wir es in der �bung besprochen haben. \n",
"//Auch das ist in der neuen Version deutlich einfacher.\n",
"|\u003e GenericChart.mapTrace( fun trace -\u003e\n",
"    let tmp = trace\n",
"    let marker = Marker.init()\n",
"    marker?colors \u003c- farben\n",
"    trace?marker \u003c- marker\n",
"    tmp\n",
") \n",
"|\u003e Chart.withTitle \"Wahlergebnisse Landtagswahl RLP 2021\"\n",
"|\u003e Chart.withSize (750.,750.)\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Task 3.2 \n",
"Vergleichen Sie die Stimmanteile aus 3.1 visuell mit mindestens 3 weiteren Landtagswahlergebnissen aus \n",
"anderen Bundeslaendern.\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 13, "outputs": [], 
           "source": ["//Relativ straight forward der code aus der Vorlesung für grouped Columns:\n",
"let datenLandtagswahl =\n",
"    [\n",
"        \"Rheinlandpfalz 2021\",\n",
"        [\n",
"            \"CDU/CSU\"               , 27.\n",
"            \"SPD\"                   , 35.\n",
"            \"Grüne\"                 , 9.3\n",
"            \"Afd\"                   , 8.3\n",
"            \"FDP\"                   , 5.5\n",
"            \"Linke\"                 , 2.5\n",
"            \"Freie Wähler/BVB/FW\"   , 5.4\n",
"            \"Die PARTEI\"            , 1.1\n",
"            \"Piraten\"               , 0.5\n",
"            \"Tierschutzpartei\"      , 1.7\n",
"            \"ÖDP\"                   , 0.7\n",
"            \"sonstige\"              , 2.4\n",
"        ]\n",
"        \"Saarland 2017\",\n",
"        [\n",
"            \"CDU/CSU\"                   ,   40.7\n",
"            \"SPD\"                       ,   29.6\n",
"            \"Grüne\"                     ,   4.0\n",
"            \"Afd\"                       ,   6.2\n",
"            \"FDP\"                       ,   3.3\n",
"            \"Linke\"                     ,   12.8\n",
"            \"Freie Wähler/BVB/FW\"      ,   0.4 \n",
"            \"Piraten\"                   ,   0.7  \n",
"            \"Die PARTEI\"                ,   0.  \n",
"            \"Tierschutzpartei\"          ,   0.  \n",
"            \"sonstige\"                  ,   2.3\n",
"        ]\n",
"        \"Baden-Würtemberg 2021\",\n",
"        [\n",
"            \"CDU/CSU\"               , 24.1\n",
"            \"SPD\"                   , 11.\n",
"            \"Grüne\"                 , 32.6\n",
"            \"Afd\"                   , 9.7\n",
"            \"FDP\"                   , 10.5\n",
"            \"Linke\"                 , 3.6\n",
"            \"Freie Wähler/BVB/FW\"   , 3.\n",
"            \"Die PARTEI\"            , 1.2\n",
"            \"Piraten\"               , 0.3\n",
"            \"Tierschutzpartei\"      , 0.\n",
"            \"ÖDP\"                   , 0.8\n",
"            \"sonstige\"              , 4.2\n",
"        ]\n",
"        \"Sachsen 2019\",\n",
"        [\n",
"            \"CDU/CSU\"               ,   32.1\n",
"            \"SPD\"                   ,   7.7 \n",
"            \"Grüne\"                 ,   8.6 \n",
"            \"Afd\"                   ,   27.5\n",
"            \"FDP\"                   ,   4.5 \n",
"            \"Linke\"                 ,   10.4\n",
"            \"Freie Wähler/BVB/FW\"  ,   3.4 \n",
"            \"Piraten\"               ,   0.3 \n",
"            \"Die PARTEI\"            ,   1.6 \n",
"            \"Tierschutzpartei\"      ,   1.5\n",
"            \"ÖDP\"                   ,   0.3\n",
"            \"sonstige\"              ,   2.4\n",
"\n",
"        ]\n",
"    ]\n",
"\n",
"let landtagswahlenChart=\n",
"    datenLandtagswahl\n",
"    |\u003e List.map\n",
"        (fun (gruppenName, daten)-\u003e \n",
"            Chart.Column(keysvalues=daten, Name=gruppenName))\n",
"    |\u003e Chart.Combine\n",
"    |\u003e Chart.withTitle \"Landtagwahlverteilung\"\n",
"    |\u003e Chart.withY_AxisStyle (\"Wahlergebnis in %\", MinMax = (0.,50.))\n",
"\n",
"landtagswahlenChart\n",
"|\u003e Chart.withSize(1000.,1000.)\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Task 3.3 \n",
"Visualisieren Sie die Daten aus 2.2 hinsichtlich ihres relativen Anteils an der Gesamtstromerzeugung als Doughnut-Chart \n",
"\n",
"_Tipp: Chart.Doughnut; Sie muessen die Ursprungsdaten jeweils durch die Gesamtmenge an erzeugten kWh Teilen._\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 14, "outputs": [], 
           "source": ["let relativeTerawattstundenDeutschland2020 =\n",
"    terawattstundenDeutschland2020\n",
"    |\u003e fun l -\u003e\n",
"        let sum = List.sumBy fst l\n",
"        l |\u003e List.map (fun (terrawatt,erzeuger) -\u003e terrawatt/sum , erzeuger)\n",
"\n",
"Chart.Doughnut(data=relativeTerawattstundenDeutschland2020)\n",
"|\u003e Chart.withTitle \"relative Terawattstundenproduktion in Deutschland je Energieträger (2020)\"\n",
"|\u003e Chart.withSize (750.,750.)\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### 4 Visualisierung von geographischer Lage\n",
"\n",
"### Task 4.1\n",
"\n",
"Visualisieren Sie den Anteil der 16 bevoelkerungsreichsten Laender an der Gesamtbevoelkerung der Erde. \n",
"\n",
"_Quelle der Daten:_ https://de.wikipedia.org/wiki/Weltbev%C3%B6lkerung#Die_bev%C3%B6lkerungsreichsten_Staaten\n",
"\n",
"_Tipp: Chart.Choropleth. Idealerweise aehnelt ihr Ergebnis der Abbildung auf Wikipedia._\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 15, "outputs": [], 
           "source": ["// Die Ländernamen müssen english sein.\n",
"// Ansonsten straight forward aus der Vorlesung:\n",
"\n",
"let locations,z = \n",
"    [\n",
"        (\"China\", 18.2);\n",
"        (\"India\", 18.1);\n",
"        (\"USA\", 5.1);\n",
"        (\"Indonesia\",3.7);\n",
"        (\"Pakistan\",2.8);\n",
"        (\"Brazil\", 2.7);\n",
"        (\"Nigeria\", 2.6);\n",
"        (\"Bangladesh\", 2.1);\n",
"        (\"Russia\",1.9);\n",
"        (\"Mexico\", 1.7);\n",
"        (\"Japan\", 1.7);\n",
"        (\"Ethopia\", 1.5);\n",
"        (\"Philippines\", 1.4);\n",
"        (\"Egypt\", 1.3);\n",
"        (\"Vietnam\", 1.2);\n",
"        (\"Deutschland\", 1.1);\n",
"    ]\n",
"    |\u003e List.unzip\n",
"\n",
"Chart.ChoroplethMap(locations,z,Locationmode=StyleParam.LocationFormat.CountryNames)\n",
"|\u003e Chart.withTitle \"Die Bevölkerungsreichsten Staaten der Welt (% Gesamtbevölkerung 2019)\"\n",
"|\u003e Chart.withSize(1000.,1000.)\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["## 5 Bonus\n",
"\n",
"Erstellen Sie ein Klimadiagramm fuer Kaiserslautern\n",
"Hierzu muessen verschiedene Charttypen miteinander kombiniert werden.\n",
"\n",
"_Quelle der Daten:_ https://de.climate-data.org/europa/deutschland/rheinland-pfalz/kaiserslautern-2135/\n",
"\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 16, "outputs": [], 
           "source": ["let meanTemp = [1.2; 1.8; 5.2; 9.6; 13.6; 17.2; 19.; 18.6; 14.8; 10.4; 5.5; 2.3]\n",
"\n",
"let precipitation = [64; 57; 62; 60; 79; 74; 78; 70; 66; 67; 68; 78]\n",
"\n",
"let months = [\"Januar\" ; \"Februar\"; \"März\"; \"April\"; \"Mai\"; \"Juni\"; \"Juli\"; \"August\"; \"September\"; \"Oktober\"; \"November\"; \"Dezember\"]\n",
"\n",
"//Temperatur kurve ähnlich der seite von der die daten kommen:\n",
"\n",
"let tempChart = \n",
"    Chart.Line (\n",
"        months,\n",
"        meanTemp, \n",
"        Name = \"Durchschnittstemperatur\",\n",
"        ShowMarkers=true\n",
"        )\n",
"    |\u003e Chart.withLineStyle(Width = 10.,Shape=StyleParam.Shape.Spline)\n",
"    |\u003e Chart.withMarkerStyle(Size = 15)\n",
"\n",
"\n",
"[\n",
"    Chart.Column (months, precipitation, Name = \"Niederschlag\")\n",
"    |\u003e Chart.withAxisAnchor(Y=1);\n",
"    tempChart\n",
"    |\u003e Chart.withAxisAnchor(Y=2)\n",
"]\n",
"|\u003e Chart.Combine \n",
"|\u003e Chart.withY_AxisStyle(\"Precipitation (mm)\", Side=StyleParam.Side.Left, Id=1, MinMax = (0.,80.)) \n",
"|\u003e Chart.withY_AxisStyle(\"Mean temperature (°C)\",Side=StyleParam.Side.Right, Id=2, Overlaying=StyleParam.AxisAnchorId.Y 1, MinMax = (0.,20.))\n",
"|\u003e Chart.withTitle \"Klimadiagramm\"\n",
"|\u003e Chart.withX_AxisStyle \"Monate\"\n",
"|\u003e Chart.withSize(1000.,1000.)\n"]
          }],
            "metadata": {
            "kernelspec": {"display_name": ".NET (F#)", "language": "F#", "name": ".net-fsharp"},
            "langauge_info": {
        "file_extension": ".fs",
        "mimetype": "text/x-fsharp",
        "name": "C#",
        "pygments_lexer": "fsharp",
        "version": "4.5"
        }
        },
            "nbformat": 4,
            "nbformat_minor": 1
        }
        

